
// Погрешности размеров отверстий подобраны для печати
// на Picaso 3D Builder с максимальным качеством 100% заполнения

// шестерня на вал мотора
// шестерня t9 на 3D Designer печатается с отверстием 2.4
// и при этом налезает на вал (с отверстием 0.2 расползается,
// хотя 2.2 меньше, чем 2.4 - так работает слайсер)
//gear_motor(print_error=0.2); // diam=2.4

// передача от мотора к колесу
//gear_transmission(print_error=0.2);

// шестерня на колесо
gear_wheel(print_error=0.1);

// Печатать с минимальной скоростью (по крайней мере мелкие t8 и t9),
// 100% заполнение, 
// тонкая настройка качества печати: обрамление детали=Нет

/** 
 * Шестеренка на вал мотора.
 * 9 зубов, шаг 1.5мм, угол зацепления 20, 
 * отверстие: круглый вал 2мм
 *
 * @param print_error погрешность при печати 3д-принтером (мм), стоит 
 * учитывать для элементов (отверстий или штырей), стыкующихся с 
 * внешними объектами (отверстие для вала при печати получится меньше
 * и в него без учета погрешности может быть будет сложно втиснуть ось)
 */
module gear_motor(print_error=0) {
  difference() {
    union() {
      // шестеренка
      linear_extrude(height=5) import(file="gear-pa20-cp1.5-t9.dxf");

      // подсадка, чтобы не расплывались зубья
      // для cp1.5-t9
      //translate([0, 0, -0.3]) cylinder(h=1, r=5.7, $fn=100);
    }

    // ось
    generic_axis(radius=1, print_error=print_error);
  }
}

/** 
 * Передача с вала на колесо (две шестерни).
 * 
 * шестерня 1: 61 зуб, шаг 1.5мм, угол зацепления 20 
 * шестерня 2: 13 зубов, шаг 3мм, угол зацепления 20
 * отверстие: круглый вал 2мм
 *
 * @param print_error погрешность при печати 3д-принтером (мм), стоит 
 * учитывать для элементов (отверстий или штырей), стыкующихся с 
 * внешними объектами (отверстие для вала при печати получится меньше
 * и в него без учета погрешности может быть будет сложно втиснуть ось)
 */
module gear_transmission(print_error=0) {
    difference() {
    union() {
      // шестеренка 1
      linear_extrude(height=3) import(file="gear-pa20-cp1.5-t61.dxf");
      // шестеренка 2
      translate([0, 0, 2]) 
        linear_extrude(height=6) import(file="gear-pa20-cp3-t13.dxf");

      // кругляш на макушке вокруг оси, чтобы упираться в стенку 0.5
      translate([0, 0, 8-0.1]) cylinder(h=0.5+0.1, r=3, $fn=100);

      // подсадка, чтобы не расплывались зубья
      translate([0, 0, -0.3]) cylinder(h=1, r=13.8, $fn=100);
    }

    // ось
    generic_axis(radius=1, print_error=print_error);
  }
}

/** 
 * Шестеренка на колесо.
 * 
 * 27 зубов, шаг 3мм, угол зацепления 20
 * отверстие - круглый вал 3мм с лыской (0.9мм от центра)
 *
 * @param print_error погрешность при печати 3д-принтером (мм), стоит 
 * учитывать для элементов (отверстий или штырей), стыкующихся с 
 * внешними объектами (отверстие для вала при печати получится меньше
 * и в него без учета погрешности может быть будет сложно втиснуть ось)
 */
module gear_wheel(print_error=0) {
  difference() {
    union() {
      // шестеренка
      linear_extrude(height=3) import(file="gear-pa20-cp3-t27.dxf");

      // кругляш на макушке вокруг оси, чтобы упираться в стенку
      translate([0, 0, 2.9]) cylinder(h=1.1, r=3, $fn=100);

      // подсадка, чтобы не расплывались зубья
      translate([0, 0, -0.3]) cylinder(h=1, r=11.6, $fn=100);
    }

    // ось с лыской
    motor2_axis(print_error=print_error);
  }
}


/**
 * Просто круглая ось, по умолчанию диаметр 3мм.
 *
 * @param print_error погрешность при печати 3д-принтером (мм), стоит 
 * учитывать для элементов (отверстий или штырей), стыкующихся с 
 * внешними объектами (отверстие для вала при печати получится меньше
 * и в него без учета погрешности может быть будет сложно втиснуть ось)
 */
module generic_axis(length=22, radius=1.5, print_error=0) {
  translate([0,0,-1])
    cylinder(h=length, r=radius+print_error, $fn=20);
}


/** 
 * Ось для для мотора 2:
 * Pololu 4.5V, 80rpm Right Angle (прямой, желтый)
 * www.robotshop.com/en/solarbotics-gm7-gear-motor-7.html
 *
 * @param radius радиус оси, диаметр оси мотора 3мм; 
 * @param cut_radius расстояние от центра до среза 
 * @param print_error погрешность при печати 3д-принтером (мм), стоит учитывать 
 * для элементов (отверстий или штырей), стыкующихся с 
 * внешними объектами (отверстие для вала при печати получится меньше
 * и в него без учета погрешности может быть будет сложно втиснуть ось)
 */
module motor2_axis(length=22, radius=1.5, cut_radius=0.9, print_error=0) {
  // диаметр 3мм, срез с одного бока 0.3мм
  translate([0,0,-1])
  difference() {
    cylinder(h=length, r=radius+print_error, $fn=20);
    // 1.5 мм "вниз" по y (совместить куб с цилиндром), 
    // 0.9 мм "вправо" по x (срезать справа, cut_radius=0.9мм)
    translate([cut_radius+print_error, -1.5, 0])
      cube([2, 3, length]);
  }
}
